apply plugin: 'java'

defaultTasks 'build'

archivesBaseName = 'parallel-ant'
version = '0.9-beta'

repositories {
    mavenCentral()
}

configurations {
    provided
}

dependencies {
    provided 'org.apache.ant:ant:1.8.1'
    
    testCompile 'junit:junit:4.8.2', 'org.jmock:jmock:2.5.1', 'org.jmock:jmock-legacy:2.5.1'
                
    testCompile('org.jmock:jmock-junit4:2.5.1'){
        exclude module: 'junit-dep'
    }
}

sourceSets.main.compileClasspath += configurations.provided
sourceSets.test.compileClasspath += configurations.provided
sourceSets.test.runtimeClasspath += configurations.provided

tasks.withType(Compile) {
    options.compilerArgs << "-Xlint:unchecked"
}

task srcZip(type: Zip) {
    classifier = 'src'
    from projectDir
    include sourceSets*.allSource.srcDirs*.collect { relativePath(it) }.flatten()
    include 'etc/**/*', 'LICENCE', 'README', 'NOTICE', 'build.*', 'gradlew*'
    exclude 'etc/eclipse'
}
assemble.dependsOn(srcZip)
